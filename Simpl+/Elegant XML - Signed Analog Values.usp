//Christopher McNeely
//Elegant Video and Soundworks

//____Compiler Directives____

#SYMBOL_NAME "Elegant XML - Signed Analog Values"
#ENABLE_STACK_CHECKING
#ENABLE_TRACE
#DEFAULT_VOLATILE
#CATEGORY "46" "Elegant Modules"
#DEFINE_CONSTANT MaxItems 50
#ANALOG_SERIAL_EXPAND AllWithAny
#HELP_PDF_FILE "Elegant XML - Signed Analog Values.pdf"

#INCLUDEPATH ""
#USER_SIMPLSHARP_LIBRARY "ElegantXML"

#Help_Begin

#Help_End

//____Inputs/Outputs
DIGITAL_INPUT _SKIP_,Initialize,_SKIP_;
ANALOG_INPUT InputValue[MaxItems];
DIGITAL_OUTPUT _SKIP_, IsInitialized;
ANALOG_OUTPUT _SKIP_,OutputValue[MaxItems];
INTEGER_PARAMETER ManagerID;
STRING_PARAMETER _SKIP_,_SKIP_,XmlPath[MaxItems][512];
#BEGIN_PARAMETER_PROPERTIES ManagerID
    propValidUnits = unitDecimal;
    propDefaultUnit = unitDecimal;
    propDefaultValue = 1d;    
    propList = {1d, "Processor 1"}, {2d, "Processor 2"}, {3d, "Processor 3"}, {4d, "Processor 4"}, {5d, "Processor 5"};
#END_PARAMETER_PROPERTIES

SignedAnalogProcessor processor;
integer initialized;

//____Functions____
callback function ProcessValueChange(integer id, signed_integer value)
{
    OutputValue[id] = value;
}

callback function ProcessIsInitialized(integer state)
{
    IsInitialized = state;
    initialized = state;
}

//____Events____

//PUSH
//RELEASE
CHANGE InputValue
{
    integer i;
    if(initialized = 0) { return; }
    i = GetLastModifiedArrayIndex();
    processor.UpdateValue(i, InputValue[i]);
}
//EVENT

//____Start-Up Code____
FUNCTION Init() {
    integer i;
    //WaitForInitializationComplete();
    while(processor.IsManagerReady(ManagerID) = 0)
    {
         Delay(100);
    }
    
    i = random(10, 500);
    delay(i);
    
    RegisterDelegate(processor, ReportValueChange, ProcessValueChange);
    RegisterDelegate(processor, ReportIsInitialized, ProcessIsInitialized);
    for(i = 1 to MaxItems)
    {
        if(IsSignalDefined(OutputValue[i]) && XmlPath[i] <> "")
        {
             processor.AddValue(i, XmlPath[i], InputValue[i]);
        }
    }   
    while(processor.Initialize(ManagerID) = 0)
    {
    	Delay(100);
    }
}
     
PUSH Initialize
{
	Init();
}
